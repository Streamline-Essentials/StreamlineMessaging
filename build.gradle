plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group properties['group']
version properties['version']

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // LuckPerms
    compileOnly 'net.luckperms:api:5.4'

    // StreamlineCore
    compileOnly 'com.github.streamline-essentials.StreamlineCore:StreamlineCore-API:64c673b4c6'
    annotationProcessor 'com.github.streamline-essentials.StreamlineCore:StreamlineCore-API:64c673b4c6'

    if (properties['include-bapi']) {
        compileOnly 'com.github.streamline-essentials.StreamlineCore:StreamlineCore-BAPI:64c673b4c6'
        annotationProcessor 'com.github.streamline-essentials.StreamlineCore:StreamlineCore-BAPI:64c673b4c6'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

shadowJar {
    manifest {
        attributes 'Plugin-Class': rootProject.properties['pf.class'].toString() + '.' + rootProject.name.toString()
        attributes 'Plugin-Id': rootProject.properties['pf.id'].toString()
        attributes 'Plugin-Version': rootProject.version.toString()
        attributes 'Plugin-Requires': ( rootProject.properties['pf.requires'].toString() == 'none' ? '' : rootProject.properties['pf.requires'].toString() )
        attributes 'Plugin-Dependencies': ( rootProject.properties['pf.dependencies'].toString() == 'none' ? '' : rootProject.properties['pf.dependencies'].toString() )
        attributes 'Plugin-Description': rootProject.properties['pf.description'].toString()
        attributes 'Plugin-Provider': rootProject.properties['pf.provider'].toString()
        attributes 'Plugin-License': rootProject.properties['pf.license'].toString()
    }

    archiveFileName = project.name + '-' + project.version + '.jar'

    minimize()
}

artifacts {
    archives shadowJar
}

wrapper {
    gradleVersion = '8.6'
    distributionType = Wrapper.DistributionType.BIN
}